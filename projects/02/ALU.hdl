// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs,
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN
        x[16], y[16],  // 16-bit inputs
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
    Not16(in=x, out=notX);
    Not16(in=y, out=notY);
    And16(a=x, b=notX, out=value0);
    Not16(in=value0, out=value1);

    Mux16(a=x, b=value0, sel=zx, out=zxResultX);
    Mux16(a=y, b=value0, sel=zy, out=zyResultY);
    Mux16(a=notX, b=value1, sel=zx, out=zxResultNotX);
    Mux16(a=notY, b=value1, sel=zy, out=zyResultNotY);

    Mux16(a=zxResultX, b=zxResultNotX, sel=nx, out=nxResultX);
    Mux16(a=zyResultY, b=zyResultNotY, sel=ny, out=nyResultY);

    Add16(a=nxResultX, b=nyResultY, out=addedXY);
    And16(a=nxResultX, b=nyResultY, out=andXY);

    Mux16(a=andXY, b=addedXY, sel=f, out=resultF);
    Not16(in=resultF, out=notResultF);
    Mux16(a=resultF, b=notResultF, sel=no, out=out,
      out[0..7]=outFormer, out[8..15]=outLatter, out[15]=ng);

    Or8Way(in=outFormer, out=formerHalfOr);
    Or8Way(in=outLatter, out=latterHalfOr);
    Or(a=formerHalfOr, b=latterHalfOr, out=reverseZR);
    Not(in=reverseZR, out=zr);
}
